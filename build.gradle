plugins {
  id 'application'
  id 'org.jetbrains.gradle.plugin.idea-ext' version '0.7'
  id 'com.google.cloud.tools.jib' version '2.7.1'
}

group 'com.jshobe.maven'
version '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
  jcenter()
}

dependencies {
  implementation 'io.undertow:undertow-servlet:2.2.3.Final'
  implementation 'software.amazon.awssdk:s3:2.15.71'
  implementation 'com.samskivert:jmustache:1.15'
  implementation('org.apache.commons:commons-configuration2:2.7') {
    exclude group: 'commons-logging'
  }
  implementation('commons-beanutils:commons-beanutils:1.9.4') {
    exclude group: 'commons-logging'
  }
  implementation 'org.yaml:snakeyaml:1.27'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.1'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'
  implementation 'com.vdurmont:semver4j:3.1.0'
  implementation 'org.mapdb:mapdb:3.0.8'
  implementation 'at.favre.lib:bcrypt:0.9.0'
  implementation 'info.picocli:picocli:4.6.1'
  compileOnly 'org.projectlombok:lombok:1.18.16'
  annotationProcessor 'org.projectlombok:lombok:1.18.16'
  runtime 'ch.qos.logback:logback-classic:1.2.3'
  runtime 'org.slf4j:jcl-over-slf4j:1.7.28'
  runtime 'com.fasterxml.woodstox:woodstox-core:6.2.3'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
  useJUnitPlatform()
}

jib {
  to {
    image 'jasonshobe/simple-maven-server'
    tags = ["$project.version"]
    auth {
      username = dockerUsername
      password = dockerPassword
    }
  }
  container {
    args = ['start']
    environment = [
        'MAVEN_STORAGE_DIRECTORY': '/var/lib/maven/repositories'
    ]
    ports = [ '8080' ]
    volumes = [ '/var/lib/maven/repositories' ]
  }
}

application {
  mainClass = 'com.jshobe.maven.cli.Main'
}

idea {
  project {
    jdkName = '1.8'
    languageLevel = '1.8'
    settings {
      delegateActions {
        delegateBuildRunToGradle = true
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
      }
      copyright {
        useDefault = 'GPL3'
        profiles {
          GPL3 {
            notice = '''\
This file is part of Simple Maven Server.
Copyright (C) $today.year  Jason Shobe

Simple Maven Server is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Simple Maven Server is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Simple Maven Server.  If not, see <https://www.gnu.org/licenses/>.'''
          }
        }
      }
    }
  }
}
